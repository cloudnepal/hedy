@top Program { eol* (Command eol+)* Command? }
Command {
    Print | Ask | Echo | Play | Turtle | ErrorInvalid
}
Print { print+ Text+ }
Play { play+ Text+ }
Ask { ask+ Text+ }
Echo { echo+ Text* }

Turtle[@isGroup=turtle] {
    Forward { forward+ Text } |
    Turn { turn+ Text } |
    Color { color+ Text }
}

ErrorInvalid[@dynamicPrecedence=-10] { Text+ }

@external extend { Text } extendKeyword from "./tokens" {
    print[@name="print"],
    forward[@name="forward"],
    turn[@name="turn"],
    echo[@name="echo"],
    play[@name="play"],
    ask[@name="ask"],
    color[@name="color"]
}


@tokens {
    Comment { "#" ![\n]* }
    eol { "\n" }
    space { " " }
    Text { ![\n #]+ }
}

@skip { Comment | space }
